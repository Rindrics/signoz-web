---
date: 2024-12-05
id: terraform-provider-signoz
title: Terraform Provider
---
# Creating SigNoz Alert using Terraform 

The configuration of alerts for a monitoring target is the very definition of what the team perceives as a “problem” and should be managed as properly as the application code.
By defining SigNoz alerts as Terraform code, it is possible to manage changes to the alert settings.

## Overview

SigNoz alert notifies you of the change in the state of your service.
Although getting alerts that work wisely often requires complex configuration, Terraform 'signoz' provider will help you manage SigNoz alerts as reviewable codes.

This document will show you how to:
- configure the provider
- create a new alert on SigNoz Cloud
- start managing preexistent alerts using Terraform

## Prerequisites

- Terraform
  - version of Terraform runtime: [v0.13.0](https://github.com/hashicorp/terraform/releases/tag/v0.13.0) or later
  - version of signoz provider: [0.0.4](https://github.com/SigNoz/terraform-provider-signoz/releases/tag/v0.0.4) or later
- SigNoz Cloud
  - endpoint URL
  - access token

## Create SigNoz Alert using Terraform

### Step 1: Configure Terraform environment

Create `.tf` file with an arbitrary name (`main.tf` is commonly used) and add the following content:
```hcl
terraform {
  required_version = ">= 0.13"
}
```

### Step 2: Install Terraform sinoz Provider

Add specification block for the signoz provider as follows:
```diff
terraform {
  required_version = ">= 0.13"

+ required_providers {
+   signoz = {
+     source = "Signoz/signoz"
+     version ~> "0.0.4"
+   }
+ }
}
```

Running `terraform init` will create `.terraform.lock.hcl`.

### Step 3: Configure sinoz Provider

Add `provider {}` block to the `.tf` file as follows:
```hcl
provider "signoz" {}
```

By setting the environmental variables below, the signoz provider will automatically read these variables and authorize with your SigNoz Cloud account:
``` bash
SIGNOZ_ACCESS_TOKEN="your-access-token"
SIGNOZ_ENDPOINT="http://your-signoz-endpoint:3301"
```

Although you can explicitly set these variables in `provider {}` as below, do not type your access token directly if you're going to push your `.tf` files to the online version control system.
```hcl
provider "signoz" {
  endpoint="http://your-signoz-endpoint:3301"
  access_token="your-access-token"  # Do not type your token directly if you push codes online
}
```

### Step 4: Define SigNoz Alert as Code

Define your alert using the `signoz_alert` resource type as follows:
```hcl
resource "signoz_alert" "example" {
  alert      = "Example alert"
  alert_type = "METRIC_BASED_ALERT"
  condition = jsonencode(
    {
      "absentFor" : 10,
      "alertOnAbsent" : true,
      "compositeQuery" : {
        "builderQueries" : {
          "A" : {
            "IsAnomaly" : false,
            "QueriesUsedInFormula" : null,
            "ShiftBy" : 0,
            "aggregateAttribute" : {
              "dataType" : "float64",
              "isColumn" : true,
              "isJSON" : false,
              "key" : "k8s_node_memory_rss",
              "type" : "Gauge"
            },
            "aggregateOperator" : "avg",
            "dataSource" : "metrics",
            "disabled" : false,
            "expression" : "A",
            "filters" : {
              "items" : [],
              "op" : "AND"
            },
            "groupBy" : [
              {
                "dataType" : "string",
                "isColumn" : false,
                "isJSON" : false,
                "key" : "k8s_node_name",
                "type" : "tag"
              }
            ],
            "limit" : 0,
            "offset" : 0,
            "pageSize" : 0,
            "queryName" : "A",
            "reduceTo" : "avg",
            "spaceAggregation" : "avg",
            "stepInterval" : 60,
            "timeAggregation" : "avg"
          }
        },
        "chQueries" : {
          "A" : {
            "disabled" : false,
            "query" : ""
          }
        },
        "panelType" : "graph",
        "promQueries" : {
          "A" : {
            "disabled" : false,
            "query" : ""
          }
        },
        "queryType" : "builder",
        "unit" : "bytes"
      },
      "matchType" : "1",
      "op" : "1",
      "selectedQueryName" : "A",
      "target" : 10,
      "targetUnit" : "gbytes"
    }
  )
  severity           = "info"
  broadcast_to_all   = false
  preferred_channels = []
  rule_type          = "threshold_rule"
  description        = "An example alert created by Terraform"
  eval_window        = "5m0s"
  frequency          = "1m0s"
  labels = {
    "environment" = "test"
  }
}
   ```
Refer to the [provider's documentation](https://registry.terraform.io/providers/SigNoz/signoz/latest/docs/resources/alert) for the latest information because the API schema of the provider may be updated.

### Step 5: Check Correctness of Definition

After defining your alert, validate its syntax by running `validate` command:
```bash
$ terraform validate
Success! The configuration is valid.
```
You'll get a success message if the resource is defined properly.

In case you get any error, fix your definition by referencing the console output:
```bash
$ terraform validate
terraform validate
╷
│ Error: Missing required argument
│
│   on main.tf line 14, in resource "signoz_alert" "example":
│   14: resource "signoz_alert" "example" {
│
│ The argument "alert_type" is required, but no definition was found.
╵
╷
│ Error: Unsupported argument
│
│   on main.tf line 16, in resource "signoz_alert" "example":
│   16:   alert_typ = "METRIC_BASED_ALERT"
│
│ An argument named "alert_typ" is not expected here. Did you mean "alert_type"?
```

### Step 6: Provision SigNoz Alert to SigNoz Cloud

When you're ready, `plan` your Terraform module to see diffs between your code and resources on the remote (SigNoz Cloud):
```bash
$ terraform plan

Terraform used the selected providers to generate the following execution plan. Resource actions are indicated with the following symbols:
  + create

Terraform will perform the following actions:

  # signoz_alert.example alert will be created
  + resource "signoz_alert" "example" {
      + alert              = "Example alert"
      + alert_type         = "METRIC_BASED_ALERT"
      + broadcast_to_all   = false
      + condition          = jsonencode(

---- snip ----

      + version            = "v4"
    }

Plan: 1 to add, 0 to change, 0 to destroy.

────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────

Note: You didn't use the -out option to save this plan, so Terraform can't guarantee to take exactly these actions if you run "terraform apply" now.
```
The output from the example code above says you're going to create an alert named "Example alert" on the remote.

Once you're satisfied that the diffs are as intended, `apply` your code for provisioning:
``` bash
$ terraform apply

Terraform used the selected providers to generate the following execution plan. Resource actions are indicated with the following symbols:
  + create

Terraform will perform the following actions:

  # signoz_alert.example will be created
  + resource "signoz_alert" "example" {
      + alert              = "Example alert"
      + alert_type         = "METRIC_BASED_ALERT"
      + broadcast_to_all   = false
      + condition          = jsonencode(

---- snip ----

      + version            = "v4"
    }

Plan: 1 to add, 0 to change, 0 to destroy.

Do you want to perform these actions?
  Terraform will perform the actions described above.
  Only 'yes' will be accepted to approve.

  Enter a value:
```
Terraform will wait for your confirmation before applying the change to the remote.

Review the diffs on the console again and type `yes` to confirm:
``` bash
  Enter a value: yes

signoz_alert.example: Creating...
signoz_alert.example: Creation complete after 1s [id=17]

Apply complete! Resources: 1 added, 0 changed, 0 destroyed.
```

After getting the success message on the console, you'll find the alert provisioned on the remote:
![Screenshot 2024-10-24 at 4.06.37](https://hackmd.io/_uploads/SJmGS0vg1g.png)


## Manage Preexistent Alert as Terraform Resource

In Terraform v1.5.0 and later, you can use an `import {}` block to import a preexistent SigNoz alert.

Define the target alert as a Terraform resource as explained in the previous section, and relate it to the resource on remote.

Specify the id of the target alert to the `id` field:
```hcl
import {
  to = signoz_alert.example
  id = "123"
}
```

Of course, you can do the same by the command:
``` bash
$ terraform import signoz_alert.example 123
```

## Conclusion

Using Terraform, you can manage both new- and preexistent- SigNoz alert as code.
The IaC practice enables you to introduce change process management of SigNoz alert and will contribute to the improvement of the operation process of your service.
